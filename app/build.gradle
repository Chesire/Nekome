apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.chesire.nekome"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "${appVersion()}"
        testInstrumentationRunner "com.chesire.nekome.TestRunner"
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix "-DEBUG"
            testCoverageEnabled !project.hasProperty("skipCoverage")
        }
        buildTypes.each {
            it.resValue("string", "version", defaultConfig.versionName)
        }
    }
    androidExtensions {
        experimental true
    }
    viewBinding {
        enabled true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude "META-INF/library-core_release.kotlin_module"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation project(path: ":account")
    implementation project(path: ":app-discover")
    implementation project(path: ":app-login")
    implementation project(path: ":app-profile")
    implementation project(path: ":app-search")
    implementation project(path: ":app-series")
    implementation project(path: ":app-settings")
    implementation project(path: ":app-timeline")
    implementation project(path: ":core")
    implementation project(path: ":database")
    implementation project(path: ":kitsu")
    implementation project(path: ":series")
    implementation project(path: ":server")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.core:core-ktx:$corektx_version"
    implementation "androidx.fragment:fragment-ktx:$fragmentctx_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.preference:preference-ktx:$preferences_version"
    implementation "androidx.work:work-runtime-ktx:2.3.4"
    implementation "com.afollestad.material-dialogs:core:$materialdialog_version"
    implementation "com.afollestad.material-dialogs:input:$materialdialog_version"
    implementation "com.afollestad.material-dialogs:lifecycle:$materialdialog_version"
    implementation "com.chesire:lifecyklelog:$lifecykle_version"
    implementation "com.chesire.lintrules:lint-gradle:$lintrules_version"
    implementation "com.chesire.lintrules:lint-xml:$lintrules_version"
    implementation "com.github.hadilq.liveevent:liveevent:$liveevent_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.5.0"
    implementation "io.coil-kt:coil:$coil_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.2"

    testImplementation project(path: ":testing")
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "junit:junit:4.13"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    androidTestImplementation project(path: ":testing")
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test:rules:1.2.0"
    // androidTestImplementation "androidx.work:work-testing:2.0.1"
    androidTestImplementation("com.schibsted.spain:barista:3.3.0") {
        exclude group: "com.android.support"
        exclude group: "org.jetbrains.kotlin"
    }
    // Fixes issue with mockk 1.9.3 https://github.com/mockk/mockk/issues/281
    androidTestImplementation("io.mockk:mockk-android:1.9.3") { exclude module: "objenesis" }
    //noinspection GradleDependency
    androidTestImplementation "org.objenesis:objenesis:2.6"

    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
