buildscript {
    ext.jacoco_version = "0.8.6"
    ext.kotlin_version = "1.4.10"
    ext.nav_version = "2.3.1"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.14.2"
    id "org.jlleitschuh.gradle.ktlint" version "9.3.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply from: "${project.rootDir}/jacoco.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: "dependencies.gradle"
apply from: "detekt.gradle"

ext.appVersion = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "describe", "--tags", "--always"
        standardOutput = stdout
    }

    def longTag = stdout.toString().trim()
    if (longTag.contains('-')) {
        return longTag.substring(longTag.indexOf('/') + 1, longTag.indexOf('-'))
    } else {
        return longTag
    }
}
