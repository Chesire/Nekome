buildscript {
    ext.jacoco_version = '0.8.5'
    ext.kotlin_version = '1.3.61'
    ext.nav_version = '2.2.1'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.5.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.1.1"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply from: "../jacoco.gradle"
}

detekt {
    toolVersion = "1.5.0"
    config = files("detekt.yml")
    input = files(
            "account/src/main/java",
            "app/src/main/java",
            "app-discover/src/main/java",
            "app-profile/src/main/java",
            "app-search/src/main/java",
            "app-series/src/main/java",
            "app-settings/src/main/java",
            "app-timeline/src/main/java",
            "core/src/main/java",
            "database/src/main/java",
            "kitsu/src/main/java",
            "series/src/main/java",
            "server/src/main/java",
            "testing/src/main/java"
    )
    reports {
        xml {
            enabled = true
            destination = file("app/build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("app/build/reports/detekt/detekt.html")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    appcompat_version = "1.1.0"
    constraint_layout_version = "2.0.0-beta4"
    corektx_version = "1.3.0-alpha01"
    coroutines_version = "1.3.3"
    dagger_version = "2.26"
    fragmentctx_version = "1.2.1"
    glide_version = "4.11.0"
    lifecycle_version = "2.2.0"
    lifecykle_version = "3.0.0"
    lintrules_version = "1.1.3"
    liveevent_version = "1.0.1"
    material_version = "1.2.0-alpha04"
    materialdialog_version = "3.1.1"
    moshi_version = "1.9.2"
    preferences_version = "1.1.0"
    room_version = "2.2.3"
    timber_version = "4.7.1"
}

ext.appVersion = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }

    def longTag = stdout.toString().trim()
    if (longTag.contains('-')) {
        return longTag.substring(longTag.indexOf('/') + 1, longTag.indexOf('-'))
    } else {
        return longTag
    }
}
